require conf/machine/include/adsp-sc598-som.inc

#Using the SPL is highly suggested, the non-SPL path is considered legacy/deprecated

# Not using SPL, comment SPL and falcon sections

# Using SPL, uncomment spl line below
MACHINE_FEATURES:append = " spl"

# Using Falcon Mode SPL, uncomment falcon line below and comment out spl above
#MACHINE_FEATURES:append = " falcon"

# Do not modify below here

# Parameters for ADI LDR tool if a recipe needs them
LDR_PROC = "SC598"
LDR_BCODE ?= "2"

MACHINE_EXTRA_RDEPENDS = "adsp-boot"

MMC_BOOT_STAGE1 = "stage1-boot.ldr"
MMC_BOOT_STAGE2 = "stage2-boot.ldr"

UBOOT_DEF_SUFFIX := "${@bb.utils.contains('MACHINE_FEATURES', 'spl', '-spl', '', d)}"
UBOOT_DEF_SUFFIX := "${@bb.utils.contains('MACHINE_FEATURES', 'falcon', '-falcon', '${UBOOT_DEF_SUFFIX}', d)}"
UBOOT_MACHINE = "sc598-som-ezlite${UBOOT_DEF_SUFFIX}_defconfig"

# use with -n "${BASH_HAS_SPL}" to test for spl build, which also includes falcon boot
# or with -z to check for not spl build
BASH_HAS_SPL = "${@bb.utils.contains_any('MACHINE_FEATURES', 'spl falcon', '1', '', d)}"
BASH_HAS_SPL_ONLY = "${@bb.utils.contains('MACHINE_FEATURES', 'spl', '1', '', d)}"
BASH_HAS_FALCON_ONLY = "${@bb.utils.contains('MACHINE_FEATURES', 'falcon', '1', '', d)}"

UBOOT_ENTRYPOINT = "0x9a080000"
UBOOT_LOADADDRESS = "0x9a080000"
UBOOT_DTBADDRESS = "0x99000000"
UBOOT_RDADDR = "0x9c000000"

KERNEL_DEVICETREE = "adi/sc598-som-ezlite.dtb"

SERIAL_CONSOLES ?= "115200;ttySC0"

IMAGE_FSTYPES = "tar.xz jffs2 wic.gz ext4"
WKS_FILE = "adsp-sc5xx.wks.in"

IMAGE_INSTALL += "mtd-utils-ubifs mtd-utils-jffs2"

EXTRA_IMAGECMD:jffs2 = "--pad --little-endian --eraseblock=0x10000 --no-cleanmarkers"

MACHINE_EXTRA_RRECOMMENDS = "kernel-modules"
BOARD = "sc598-som-ezlite"
